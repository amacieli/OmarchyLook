import QtQuick
import QtQuick.Layouts
import qs.Commons
import qs.Ui
import "nav"
import "mail"
import "chrome"
import "auth"
import "settings"
import "views"
import "state"
import "common"
import "keys"

// Window content: wires the state object to the presentational components and
// owns keyboard dispatch. No styling or backend logic lives here.
FocusScope {
  id: root

  signal closeRequested()

  function focusKeys() { keyCatcher.forceActiveFocus() }

  AppState {
    id: appState
    onFocusRequested: root.focusKeys()
    onCloseRequested: root.closeRequested()
  }

  Component.onCompleted: root.focusKeys()

  // ---- keyboard: registry (what each key does) + router (which key, which scope)
  AppCommands {
    id: commands
    app: appState
    content: contentLoader.item
    folderPageRows: folderBar.pageRows
    onPaletteRequested: function(prefill) { palette.show(prefill) }
    onHelpRequested: help.show()
    onQuitRequested: root.closeRequested()
  }

  CommandPalette {
    id: palette
    anchors.fill: parent
    z: 950
    commands: commands.list
    scopes: keyCatcher.scopes
    onClosed: root.focusKeys()
  }

  HelpOverlay {
    id: help
    anchors.fill: parent
    z: 960
    commands: commands.list
    scopes: keyCatcher.scopes
    onClosed: root.focusKeys()
  }

  KeyRouter {
    id: keyCatcher
    anchors.fill: parent
    commands: commands.list

    // Innermost first: the pane, "<view>/<pane>", the view, then everything.
    readonly property string paneScope: appState.focusPane === "msg" ? "list" : appState.focusPane
    scopes: [paneScope, appState.currentView + "/" + paneScope, appState.currentView, "global"]

    // A yes/no question (permanent delete) takes every key until it is answered.
    interceptor: function(e) {
      if (!appState.pendingConfirm) return false
      confirmDialog.handleKey(e)
      return true
    }

    // Hand keys to text inputs / popups while they own focus.
    blocked: topBar.searchFocused || settingsEditing || appState.auth.showModal || palette.open || help.open

    // A page (settings fields, the mail view's image dropdown, compose) owns the keyboard.
    readonly property bool settingsEditing: contentLoader.item && contentLoader.item.editing === true

    // Esc must still dismiss the auth modal even though `blocked` is set.
    Keys.onEscapePressed: function(event) {
      event.accepted = appState.auth.showModal
      if (event.accepted) appState.auth.cancel()
    }

    ColumnLayout {
      anchors.fill: parent
      spacing: 0

      TopStatusBar {
        id: topBar
        Layout.fillWidth: true
        backendOnline: appState.backendOnline
        isAuthenticated: appState.auth.isAuthenticated
        viewLabel: appState.currentViewLabel
        onAccountClicked: appState.openSettingsCategory("account")
      }

      PanelSeparator { Layout.fillWidth: true }

      RowLayout {
        Layout.fillWidth: true
        Layout.fillHeight: true
        Layout.margins: Style.spacing.sm
        spacing: Style.spacing.sm

        NavBar {
          Layout.preferredWidth: implicitWidth
          Layout.fillHeight: true
          items: appState.navItems
          currentIndex: appState.navIndex
          expanded: appState.sidebarExpanded
          paneFocused: appState.focusPane === "nav"
          cursorOnToggle: appState.navOnToggle
          onItemClicked: function(i) { appState.clickNav(i) }
          onToggleClicked: appState.clickNavToggle()
        }

        FolderBar {
          id: folderBar
          visible: appState.currentView === "mail" && appState.showFolderPane
          Layout.preferredWidth: visible ? implicitWidth : 0
          Layout.fillHeight: true
          model: appState.folderModel
          currentIndex: appState.folderIndex
          selectedId: appState.selectedFolderId
          paneFocused: appState.focusPane === "folder"
          onFolderClicked: function(i) { appState.clickFolder(i) }
          onRefreshRequested: { appState.loadFolders(); appState.focusRequested() }
        }

        // Mail draws its own two frames (list, reader); every other view is one pane.
        Item {
          Layout.fillWidth: true
          Layout.fillHeight: true

          Loader {
            id: contentLoader
            anchors.fill: parent
            // Non-mail views sit inside the frame; mail's panes draw their own.
            anchors.margins: appState.currentView === "mail" ? 0 : 2
            anchors.topMargin: appState.currentView === "mail" ? 0 : Style.spacing.xl
            sourceComponent: {
              switch (appState.currentView) {
                case "settings": return settingsComponent
                case "calendar": return calendarComponent
                case "contacts": return peopleComponent
                case "tasks":    return tasksComponent
                case "sms":      return smsComponent
                default:         return mailComponent
              }
            }
          }

          PaneFrame {
            visible: appState.currentView !== "mail"
            focused: appState.focusPane === "msg"
            hotkey: "\u00b2"
            title: appState.currentViewLabel
          }
        }
      }

      PanelSeparator { Layout.fillWidth: true }

      BottomStatusBar {
        Layout.fillWidth: true
        backendOnline: appState.backendOnline
        viewLabel: appState.currentViewLabel
        folderName: appState.selectedFolderName
        messageCount: appState.messageTotal
        unreadCount: appState.unreadCount
        focusPane: appState.focusPane
        currentView: appState.currentView
        pendingText: keyCatcher.pendingText
        pendingOptions: keyCatcher.pendingOptions
        notice: commands.notice
        markCount: appState.markCount
      }
    }

    Component { id: mailComponent;     MailView { app: appState } }
    Component { id: calendarComponent; CalendarView { mode: appState.calendarMode; onModeChanged: appState.calendarMode = mode; monoFont: Style.font.family; accentColor: Color.accent; successColor: Color.accent; dangerColor: Color.urgent; textColor: Color.foreground } }
    Component { id: peopleComponent;   PeopleView   { viewMode: appState.peopleView; onViewModeChanged: appState.peopleView = viewMode; sortKey: appState.peopleSort; onSortKeyChanged: appState.peopleSort = sortKey; monoFont: Style.font.family; accentColor: Color.accent; successColor: Color.accent; dangerColor: Color.urgent; textColor: Color.foreground } }
    Component { id: tasksComponent;    TasksView    { monoFont: Style.font.family; accentColor: Color.accent; successColor: Color.accent; dangerColor: Color.urgent; textColor: Color.foreground } }
    Component { id: smsComponent;      SmsView {} }
    Component {
      id: settingsComponent
      SettingsView {
        app: appState
        onLoginRequested: function(provider) { appState.auth.startLogin(provider) }
        onAccountLoginRequested: function(accountId, provider) { appState.auth.loginAccount(accountId, provider) }
        onLogoutRequested: function(accountId) { appState.auth.logout(accountId) }
        onRemoveRequested: function(accountId) { appState.auth.removeAccount(accountId) }
      }
    }
  }

  // Pops up after a short pause on a half-typed chord such as `g`.
  WhichKey {
    anchors.left: parent.left
    anchors.bottom: parent.bottom
    anchors.leftMargin: Style.spacing.huge
    anchors.bottomMargin: Style.space(40)
    z: 900
    pendingText: keyCatcher.pendingText
    options: keyCatcher.pendingOptions
  }

  ConfirmDialog {
    id: confirmDialog
    anchors.fill: parent
    z: 1100
    opened: appState.pendingConfirm !== null
    message: appState.pendingConfirm ? appState.pendingConfirm.message : ""
    confirmText: appState.pendingConfirm ? appState.pendingConfirm.confirmText : "Confirm"
    // Enter must never destroy mail by accident: the dialog opens on Cancel.
    onOpenedChanged: if (opened) selectedIndex = 0
    onCanceled: appState.pendingConfirm = null
    onConfirmed: { var c = appState.pendingConfirm; appState.pendingConfirm = null; if (c) c.run() }
  }

  Connections {
    target: appState
    function onNotify(text) { commands.say(text) }
  }

  ActionToast {
    id: actionToast
    app: appState
    anchors.right: parent.right
    anchors.bottom: parent.bottom
    anchors.rightMargin: Style.spacing.huge
    anchors.bottomMargin: Style.space(40) + (sendToast.visible ? sendToast.height + Style.spacing.md : 0)
    z: 900
  }

  SendToast {
    id: sendToast
    app: appState
    anchors.right: parent.right
    anchors.bottom: parent.bottom
    anchors.rightMargin: Style.spacing.huge
    anchors.bottomMargin: Style.space(40)      // clear of the bottom status bar
    z: 900
  }

  AuthModal {
    anchors.fill: parent
    auth: appState.auth
    z: 1000
  }

  ReauthConfirm {
    anchors.fill: parent
    auth: appState.auth
    z: 1001
  }
}
